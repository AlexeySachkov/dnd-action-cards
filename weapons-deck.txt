LINK = Weapons.csv
LINK = character-a.csv

CARDSIZE = 5.7, 8.9
DPI = 300

; List character's inventory here
[inventory] = RANGELABEL("Greatsword,Shortbow,Club,Dagger")
[to_use] = RANGELABEL(LOOKUP([inventory], [Name]))
; Remove the line below to print all the cards
; TODO: check if it is possible to use LINKFILTER instead
PRINT = [to_use]

BORDER = ROUNDED, #404040, 0.15, SOLID

FONT="Mr Eaves SC Remake Medium",12,T,#000000
TEXT = , [Name], 0, 0, 100%, 10%

; Icon on the left describing whether this is an Action or a Bonus Action
; For now only generating main hand/two-hands attacks, which are always Action
; TODO: somehow generate second card for light weapons with Bonus Action attack
; using a second hand
; POLYGON = , 0.3, 0.2, 0.6, 0.6, 3, 0, #FF6600
CIRCLE = , 0.3, 0.2, 0.5, 0.5, #006600

LINE = , 0.2, 0.81, 5.5, 0.81, #000000, 0.02

FONT="Bookinsanity Remake",10,B,#000000
TEXT = , "Attack", 0.2, 1, 30%, 10%, left, top
FONT="Bookinsanity Remake",8,IT,#000000
IF = "two-handed" @ [Properties]
TEXT = , " (with two hands)", 1.3, 1.06, 50%, 10%, left, top
ELSE
TEXT = , " (with the main hand)", 1.3, 1.06, 50%, 10%, left, top
ENDIF

; TODO: account for finesse weapons which use higher of STR/DEX
[ATK_ability] = JOINIF(,"ranged", @, [Type], [STR], [DEX])

; TODO: A character may not be proficient with a weapon
[ATK_MOD] = ROUND({([ATK_ability] - 10) / 2 + [ProficiencyBonus]}, 0, DOWN)
[sign] = JOINIF(,[ATK_MOD], >, 0, " + ", "")
[sign] = JOINIF([sign], [ATK_MOD], <, 0, " - ", "")
FONT="Bookinsanity Remake",10,BT,#000000
; Note: 'JOIN' should be used instead of 'CONCAT' is sequences are involved
[ATK_TEXT] = JOIN("d20 ", [sign], CALC(A, [ATK_MOD]))
TEXT = , [ATK_TEXT], 0, 1.45, 100%, 10%, center, top
IMAGE = , d20.png, 1.7, 1.4, 0.5, 0.5, 0, P



FONT="Bookinsanity Remake",10,BT,#000000
TEXT = , "Damage", 0.2, 1.9, 100%, 20%, left, top
FONT="Bookinsanity Remake",8,IT,#000000
TEXT = , JOIN("(", [DamageType], ")"), 1.6, 1.67, 50%, 10%, left top

[dice_prefix] = JOINIF(,[DamageDiceMultiplier], >, 1, [DamageDiceMultiplier],)
[DMG_MOD] = ROUND({([ATK_ABILITY] - 10) / 2}, 0, DOWN)
[sign] = JOINIF(,[DMG_MOD], >, 0, " + ", "")
[sign] = JOINIF([sign], [DMG_MOD], <, 0, " - ", "")
[DAMAGE_TEXT] = JOIN([dice_prefix], "d", [DamageDice], [sign], CALC(A, [DMG_MOD]))
FONT="Bookinsanity Remake",10,BT,#000000
[img_path] = JOIN("d", [DamageDice], ".png")
; TODO: if formula is long, it may collide with the dice icon
IMAGE = , [img_path], 1.7, 2.3, 0.5, 0.5, 0, P
TEXT = , [DAMAGE_TEXT], 0, 2.35, 100%, 10%, center, top



; TODO: move this footer a bit lower
LINE = , 0.2, 7.9, 5.5, 7.9, #000000, 0.02
FONT="Bookinsanity Remake",10,IT,#000000

[UPD_Type] = CASESTRING([Type], L)
[UPD_Proficiency] = CASESTRING([Proficiency], F)
[top_line] = JOIN([UPD_Proficiency], " ", [UPD_Type], " weapon")
TEXT = , [top_line], 0, 7.9, 100%, 10%, center, top
; ";" can't be used directly, but instead its ANSII encoding should be used
[UPD_Properties] = CASESTRING(REPLACE([Properties], "\59\", ", "), F)
TEXT = , [UPD_Properties], 0, 8.25, 100%, 10%, center, top

