LINK = data\Weapons.csv
LINK = data\Orianna.csv

PAGE = 21, 29.7, LANDSCAPE
CARDSIZE = 5.7, 8.9
DPI = 300

FOOTER = , [CharacterName], RIGHT

; List character's inventory here (comma-separated)
[inventory] = RANGELABEL("Greataxe,Handaxe,Javelin,Scimitar,Quarterstaff,Blowgun")
[to_use] = RANGELABEL(LOOKUP([inventory], [Name]))
; Remove the line below to print all the cards
; TODO: check if it is possible to use LINKFILTER instead
PRINT = [to_use]

BORDER = ROUNDED, #404040, 0.15, SOLID

FONT="Mr Eaves SC Remake Medium",12,T,#000000
TEXT = , [Name], 0, 0, 100%, 10%

; Icon on the left describing whether this is an Action or a Bonus Action
; For now only generating main hand/two-hands attacks, which are always Action
; TODO: somehow generate second card for light weapons with Bonus Action attack
; using a second hand
; POLYGON = , 0.3, 0.2, 0.6, 0.6, 3, 0, #FF6600
CIRCLE = , 0.3, 0.2, 0.5, 0.5, #006600

LINE = , 0.2, 0.81, 5.5, 0.81, #000000, 0.02

HTMLFONT = B10, "Bookinsanity Remake", 10, B, #000000
HTMLFONT = I8, "Bookinsanity Remake", 8, I, #000000

; Image d0 is a placeholders for special weapons that have no damage,
; or if their damage is not associated with any dice.
; TODO: align images with the text better. It may require using a different
; environment
HTMLIMAGE = , dice_img0, "assets\d0.png", 0.5, 0.5, P
HTMLIMAGE = , dice_img4, "assets\d4.png", 0.5, 0.5, P
HTMLIMAGE = , dice_img6, "assets\d6.png", 0.5, 0.5, P
HTMLIMAGE = , dice_img8, "assets\d8.png", 0.5, 0.5, P
HTMLIMAGE = , dice_img10, "assets\d10.png", 0.5, 0.5, P
HTMLIMAGE = , dice_img12, "assets\d12.png", 0.5, 0.5, P
HTMLIMAGE = , dice_img20, "assets\d20.png", 0.5, 0.5, P

IF = INDEX("two-handed", [Properties]) > 0
HTMLTEXT = , "<B10>Attack</B10> <I8>(use both hands)</I8>", 0.2, 1, 80%, 10%
ELSEIF = INDEX("thrown", [Properties]) > 0
HTMLTEXT = , "<B10>Attack/throw</B10> <I8>(use the main hand)</I8>", 0.2, 1, 100%, 10%
ELSE
HTMLTEXT = , "<B10>Attack</B10> <I8>(use the main hand)</I8>", 0.2, 1, 80%, 10%
ENDIF

[ATK_ability_flag] = JOINIF(, "ranged", @, [Type], "D", "S")
; Contains (@) doesn't work here, because it expects a sequence on the right,
; but a weapon properties are represented as a single string
; [ATK_ability_flag] = JOINIF([ATK_ability_flag], "finesse", @, [Properties], "D")
[ATK_ability_flag] = JOINIF([ATK_ability_flag], INDEX("finesse", [Properties]), >, 0, "D")

; ranged weapons
[ATK_ability] = JOINIF(, [ATK_ability_flag], =, "D", [DEX])
; finesse ranged weapons
[ATK_ability] = JOINIF([ATK_ability], [ATK_ability_flag], =, "DD", CALC(M, [STR], [DEX]))
; meleee weapons
[ATK_ability] = JOINIF([ATK_ability], [ATK_ability_flag], =, "S", [STR])
; finesse melee weapons
[ATK_ability] = JOINIF([ATK_ability], [ATK_ability_flag], =, "SD", CALC(M, [STR], [DEX]))


; TODO: A character may not be proficient with a weapon
[ATK_MOD] = ROUND({([ATK_ability] - 10) / 2 + [ProficiencyBonus]}, 0, DOWN)
[sign] = JOINIF(,[ATK_MOD], >, 0, " + ", "")
[sign] = JOINIF([sign], [ATK_MOD], <, 0, " - ", "")

; Note: 'JOIN' should be used instead of 'CONCAT' is sequences are involved
HTMLKEY = , "attack_formula", JOIN("d20 ", [sign], CALC(A, [ATK_MOD])), B10
HTMLTEXT = , "<center>dice_img20 attack_formula</center>", 0, 1.45, 100%, 10%
HTMLTEXT = , "<B10>Damage</B10> <I8>({{DamageType?}})</I8>", 0.2, 1.95, 80%, 10%

; TODO: weapons with no damage (Net)
[dice_prefix] = JOINIF(,[DamageDiceMultiplier], >, 1, [DamageDiceMultiplier],)
[DMG_MOD] = ROUND({([ATK_ability] - 10) / 2}, 0, DOWN)
[sign] = JOINIF(,[DMG_MOD], >, 0, " + ", "")
[sign] = JOINIF([sign], [DMG_MOD], <, 0, " - ", "")
IF = [DamageDiceMultiplier] > 0
HTMLKEY = , "damage_formula", JOIN([dice_prefix], "d", [DamageDice], [sign], CALC(A, [DMG_MOD])), B10
HTMLTEXT = , "<center>dice_img{{DamageDice?}} damage_formula</center>", 0, 2.4, 100%, 10%
ELSE
; TODO: should ability modifier be added to such weapons? (Blowgun)
HTMLKEY = , "damage_formula", "{{DamageDice?}}", B10
HTMLTEXT = , "<center>damage_formula</center>", 0, 2.4, 100%, 10%
ENDIF

[versatile_dice_prefix] = JOINIF(,[VersatileDamageDiceMultiplier], >, 1, [VersatileDamageDiceMultiplier])
HTMLKEY = , "versatile_formula", JOIN([versatile_dice_prefix], "d", [VersatileDamageDice], [sign], CALC(A, [DMG_MOD])), B10

IF = INDEX("versatile", [Properties]) > 0
HTMLTEXT = , "<B10>Or, attack</B10> <I8>(use both hands)</I8>", 0.2, 3.15, 80%, 10%
HTMLTEXT = , "<center>dice_img20 attack_formula</center>", 0, 3.6, 100%, 10%
HTMLTEXT = , "<B10>Damage</B10> <I8>({{DamageType?}})</I8>", 0.2, 4.1, 80%, 10%
HTMLTEXT = , "<center>dice_img{{VersatileDamageDice?}} versatile_formula</center>", 0, 4.5, 100%, 10%
; TODO: versatile thrown weapons (Trident)
ENDIF

; TODO: minor action card for light weapons in the second hand

; TODO: display cost and weight somehow?

; TODO: move this footer a bit lower
LINE = , 0.2, 7.9, 5.5, 7.9, #000000, 0.02
FONT="Bookinsanity Remake",10,IT,#000000
; TODO: display range
[UPD_Type] = CASESTRING([Type], L)
[UPD_Proficiency] = CASESTRING([Proficiency], F)
[top_line] = JOIN([UPD_Proficiency], " ", [UPD_Type], " weapon")
TEXT = , [top_line], 0, 7.9, 100%, 10%, center, top
; ";" can't be used directly, but instead its ANSII encoding should be used
[UPD_Properties] = CASESTRING(REPLACE([Properties], "\59\", ", "), F)
TEXT = , [UPD_Properties], 0, 8.25, 100%, 10%, center, top

